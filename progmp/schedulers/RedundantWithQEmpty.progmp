SCHEDULER RedundantWithQEmpty;

VAR sbfCandidates = SUBFLOWS.FILTER(sbf => sbf.CWND > sbf.SKBS_IN_FLIGHT + sbf.QUEUED AND !sbf.THROTTLED AND !sbf.LOSSY);

IF(sbfCandidates.EMPTY) { RETURN; }

IF (!RQ.EMPTY) {
  VAR sbfCandidate = sbfCandidates.FILTER(sbf => sbf.HAS_WINDOW_FOR(RQ.TOP) AND !RQ.TOP.SENT_ON(sbf)).MIN(sbf => sbf.RTT);
  IF (sbfCandidate != NULL) {
    sbfCandidate.PUSH(RQ.POP());
    RETURN;
  }
}

IF (!Q.EMPTY) {
  sbfCandidates.FILTER(sbf => sbf.HAS_WINDOW_FOR(Q.TOP)).MIN(sbf => sbf.RTT).PUSH(Q.POP());
} ELSE {
  /* retransmit on all other subflows... start with oldest skb which was not sent on a sbf which has cwnd */

  VAR skbCandidate = QU.
    FILTER(skb_ => 
      !sbfCandidates.FILTER(sbf => ! skb_.SENT_ON(sbf)).EMPTY
    ).TOP;

  sbfCandidates.FILTER(sbf => !skbCandidate.SENT_ON(sbf)).MIN(sbf => sbf.RTT).PUSH(skbCandidate);
}